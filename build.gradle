buildscript {
    repositories {
        maven { url "https://repo.spring.io/plugins-release" }
    }
}

plugins {
    id 'java'
    id 'checkstyle'
    id "io.spring.dependency-management" version "1.0.6.RELEASE" apply false
    id "io.freefair.lombok" version "5.3.0" apply false
}

ext {
    assertjVersion = '3.8.0'
    checkstyleVersion = '8.38'
    commonsCliVersion = '1.4'
    commonsConfigurationVersion = '1.10'
    commonsIoVersion = '2.5'
    commonsLang3Version = '3.6'
    commonsTextVersion = '1.6'
    guavaVersion = '22.0'
    jacksonVersion = '2.9.7'
    javassistVersion = '3.12.1.GA'
    junitVersion = '4.12'
    restAssuredVersion = '2.9.0'
    slf4jVersion = '1.7.12'
}

allprojects {
    apply plugin: "io.spring.dependency-management"

    group = 'pl.jdata.statest'
    version = '0.0.2-SNAPSHOT'

    dependencyManagement {
        dependencies {
            dependency "com.google.guava:guava:$guavaVersion"
            dependency "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
            dependency "com.jayway.restassured:rest-assured:$restAssuredVersion"
            dependency "com.puppycrawl.tools:checkstyle:$checkstyleVersion"
            dependency "commons-cli:commons-cli:$commonsCliVersion"
            dependency "commons-configuration:commons-configuration:$commonsConfigurationVersion"
            dependency "commons-io:commons-io:$commonsIoVersion"
            dependency "javassist:javassist:$javassistVersion"
            dependency "junit:junit:$junitVersion"
            dependency "org.apache.commons:commons-lang3:$commonsLang3Version"
            dependency "org.apache.commons:commons-text:$commonsTextVersion"
            dependency "org.assertj:assertj-core:$assertjVersion"
            dependency "org.slf4j:slf4j-log4j12:$slf4jVersion"
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'io.freefair.lombok'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    generateLombokConfig.enabled = false

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs = ['-Xlint:unchecked']
    }

    repositories {
        jcenter()
    }

    checkstyle {
        configFile = rootProject.file("config/checkstyle/checkstyle.xml")
        configProperties = ["suppressionFile": rootProject.file('config/checkstyle/suppressions.xml')]
        toolVersion = checkstyleVersion
    }

    dependencies {
        checkstyle 'com.puppycrawl.tools:checkstyle'

        testImplementation 'junit:junit'
    }
}
